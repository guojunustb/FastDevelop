apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
Properties props = new Properties()
props.load(new FileInputStream(file("signing.properties")))
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    //让debug也使用正式签名,方便调试,比如qq和微信就得正式签名才能进行调试
    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }
    defaultConfig {
        applicationId "com.fastdevtest"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    applicationVariants.all { variant ->
        def outputFile = variant.outputs[0].outputFile;
        if (outputFile != null && outputFile.name.endsWith('.apk')) {
            // Out apk name 'yzb-baidu-V2.0.0.150720-debug.apk
            def fileName = "FastDevelop-${variant.productFlavors[0].name}-V${defaultConfig.versionName}-${variant.buildType.name}.apk"
            variant.outputs[0].outputFile = new File(outputFile.parent, fileName)
        }
    }
    productFlavors {
        def path = "channel_debug.txt"
        file(path).eachLine { line ->
            def words = line.split(':')
            def channel = words[0]
            def umengKey = words[1]
            if (channel == '') {
                channel = umengKey
            }
            def name = channel
            "$name" {
                manifestPlaceholders = [CHANNEL_VALUE: channel, UMENG_KEY_VALUE: umengKey]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
        debug {
            signingConfig signingConfigs.release
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.alibaba:fastjson:1.2.8'
    compile 'com.orhanobut:logger:1.11'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.guava:guava:19.0'
    compile 'com.facebook.fresco:fresco:0.9.0'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:retrofit-converters:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile files('libs/sun.misc.BASE64Decoder.jar')

}